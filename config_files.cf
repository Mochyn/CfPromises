body common control
{
	bundlesequence => {  "inputs" } ;
	inputs => { @(inputs.files) };
	bundlesequence => { "configfiles" };
}

bundle common inputs
{
	vars:
		"files" slist => {"/var/cfengine/inputs/libraries/cfengine_stdlib.cf" };
}

bundle agent configfiles
{
	vars:
		#Files to Edit
		"files[sysctl]" 	string => "/etc/sysctl.conf";
		"files[sshd]"		string => "/etc/ssh/ssh_config";
		"files[inittab]"	string => "/etc/inittab";

		#Sysctl variables to set
		"sysctl[net.ipv4.tcp_syncookies]"		string => "1";
		"sysctl[net.ipv4.conf.all.accept_source_route]"	string => "0";
		"sysctl[net.ipv4.conf.all.accept_redirects]"	string => "0";
		"sysctl[net.ipv4.conf.all.rp_filter]"		string => "1";
		"sysctl[net.ipv4.conf.all.log_martians]"	string => "1";

		#SSHD configurations to set
		"sshd[protocol]"				string => "2";
		"sshd[X11Forwading]"				string => "yes";
		"sshd[UseDNS]"					string => "no";

	methods:
		"sysctl"	usebundle => edit_sysctl;
		"sshd"		usebundle => edit_sshd;
		"inittab"	usebundle => edit_inittab;
}

bundle agent edit_sysctl
{
	files:
		"$(configfiles.files[sysctl])"
			handle => "edit_sysctl",
			comment => "Make sure sysctl contains desired configuration settings",
			create => "true",
			edit_line => set_variable_values("configfiles.sysctl"),
			classes => if_repaired("sysctl_modified");

	commands:
		sysctl_modified.!no_restarts::
			"/sbin/sysctl -p"
				handle => "reload_sysctl",
				comment => "Make sure new sysctl settings are loaded";
}

bundle agent edit_sshd
{
	files:
		"$(configfiles.files[sshd])"
			handle => "edit_sshd",
			comment => "Set desired sshd config parameters",
			edit_line => set_config_values("configfiles.sshd"),
			classes => if_repaired("restart_sshd");
	commands:
		restart_sshd.!no_restarts::
			"/etc/init.d/ssh reload"
				handle => "sshd_restart",
				comment => "Restart sshd if the configuration file was modified";
}

bundle agent edit_inittab
{
	files:
		"$(configfiles.files[inittab])"
			handle => "inittab_set_initdefault",
			comment => "Ensure graphical mode is disabled (default runmode=3)",
			create => "false",
			edit_defaults => backup_timestamp,
			edit_line => set_colon_field("id", "2", "3");
		"$(configfiles.files[inittab])"
			handle => "inittab disable crtlaltdel",
			comment => "Ensure handling of crtl-alt-del i disabled",
			create => "false",
			edit_defaults => backup_timestamp,
			edit_line => comment_lines_matching("ca::ctrlaltdel:.*", "#");
}
